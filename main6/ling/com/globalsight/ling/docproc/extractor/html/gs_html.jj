//  -*- Mode: Java -*-
/*
 * Copyright (c) 2000 GlobalSight Corporation. All rights reserved.
 *
 * THIS DOCUMENT CONTAINS TRADE SECRET DATA WHICH IS THE PROPERTY OF
 * GLOBALSIGHT CORPORATION. THIS DOCUMENT IS SUBMITTED TO RECIPIENT
 * IN CONFIDENCE. INFORMATION CONTAINED HEREIN MAY NOT BE USED, COPIED
 * OR DISCLOSED IN WHOLE OR IN PART EXCEPT AS PERMITTED BY WRITTEN
 * AGREEMENT SIGNED BY AN OFFICER OF GLOBALSIGHT CORPORATION.
 *
 * THIS MATERIAL IS ALSO COPYRIGHTED AS AN UNPUBLISHED WORK UNDER
 * SECTIONS 104 AND 408 OF TITLE 17 OF THE UNITED STATES CODE.
 * UNAUTHORIZED USE, COPYING OR OTHER REPRODUCTION IS PROHIBITED
 * BY LAW.
 */

/*
Javacc grammar file for HTML
Author: Jim Hargrave
Version 1.0 1/26/2000 Initial version

Extended by Cornelis van der Laan
Version 2.0 3/19/2001 Coldfusion tag additions

Extended by Cornelis van der Laan
Version 3.0 11/9/2002 ColdFusion rewrite for embedded tag support,
                      recursive comments and strings inside strings.
*/

options
{
    IGNORE_CASE = true;
    STATIC = false;
    JAVA_UNICODE_ESCAPE = false;
    UNICODE_INPUT = true;
    DEBUG_PARSER = false;
    DEBUG_TOKEN_MANAGER = false;
    DEBUG_LOOKAHEAD = false;
}

/*
options
{
  LOOKAHEAD = 1;
  CHOICE_AMBIGUITY_CHECK = 2;
  OTHER_AMBIGUITY_CHECK = 1;
  STATIC = false;
  DEBUG_PARSER = false;
  DEBUG_LOOKAHEAD = false;
  DEBUG_TOKEN_MANAGER = false;
  ERROR_REPORTING = true;
  JAVA_UNICODE_ESCAPE = false;
  UNICODE_INPUT = false;
  IGNORE_CASE = true;
  USER_TOKEN_MANAGER = false;
  USER_CHAR_STREAM = false;
  BUILD_PARSER = true;
  BUILD_TOKEN_MANAGER = true;
  SANITY_CHECK = true;
  FORCE_LA_CHECK = true;
}
*/

PARSER_BEGIN(Parser)
package com.globalsight.ling.docproc.extractor.html;

import com.globalsight.ling.docproc.extractor.html.HtmlObjects;
import com.globalsight.ling.docproc.extractor.html.IHtmlHandler;

import java.util.ArrayList;
import java.util.Enumeration;
import java.util.Iterator;

/**
 * <p>A HTML Parser automatically generated by JavaCC.  Builds an HTML
 * Object Model and sends events to abstract handler methods in type
 * {@link IHtmlHandler}, which are implemented by class
 * ExtractionHandler.
 *
 * @see HtmlObjects
 * @see IHtmlHandler
 * @see ExtractionHandler
 */
public class Parser
    implements HtmlObjects
{
    IHtmlHandler m_handler = null;
    boolean ignoreInvalidHtmlTags = false;
	String jsFunctionText = null;

    public static void main(String args[])
        throws ParseException
    {
        Parser parser = new Parser (System.in);
        parser.parse();
    }

    public void setHandler(IHtmlHandler p_handler)
    {
        m_handler = p_handler;
    }
    
    public void setIgnoreInvalidHtmlTags(boolean ignore)
    {
        ignoreInvalidHtmlTags = ignore;
    }

	public void setJsFunctionText(String jsFunctionText)
	{
		this.jsFunctionText = jsFunctionText;
	}
    /**
     * <p>Return the current line number in the input file.  This is a
     * little helper that allows us to offset error positions obtained
     * from embedded extractors.  Embedded extractors receive a fresh
     * input object and start counting lines and columns from 1.</p>
     */
    public int getCurrentLine()
    {
        return jj_input_stream.line;
    }

    /**
     * <p>Return the current column number in the input file.  This is a
     * little helper that allows us to offset error positions obtained
     * from embedded extractors.  Embedded extractors receive a fresh
     * input object and start counting lines and columns from 1.</p>
     */
    public int getCurrentColumn()
    {
        return jj_input_stream.column;
    }

    /**
     * Returns the original text including whitespace that occured
     * between two tokens. Leading whitespace before the first token
     * is *not* returned. The tokens cannot be the same.
     */
    private String getTokenText(Token first, Token last)
    {
        StringBuffer res = new StringBuffer();

        res.append(first.image);

        for (Token t = first.next; t != last.next; t = t.next)
        {
            if (t.specialToken != null)
            {
                Token tt = t.specialToken;

                // walk the list backwards
                while (tt.specialToken != null)
                {
                    tt = tt.specialToken;
                }

                // and forwards again
                for (; tt != null; tt = tt.next)
                {
                    res.append(tt.image);
                }
            }

            res.append(t.image);
        }

        return res.toString();
    }
}
PARSER_END(Parser)

/* LEXICAL DEFINITIONS */

<*> TOKEN :
{
  <#ALPHA_CHAR: ["a"-"z", "A"-"Z"] >
| <#NUM_CHAR:   ["0"-"9"] >
| <#ALPHANUM_CHAR: [ "a"-"z", "A"-"Z", "0"-"9" ] >
// Identifier chars for tag and attribute names:
// ":" is for namespace separators in tags & attributes,
// "#" is for ColdFusion variable substitutions.
| <#IDENTIFIER_CHAR:
    [ "a"-"z", "A"-"Z", "0"-"9", "_", "-", ".", ":", "#"] >
| <#IDENTIFIER: <IDENTIFIER_CHAR> (<IDENTIFIER_CHAR>)* >
| <#QUOTED_STRING:
    ( "'" ( ~["'"] )* "'" ) | ( "\"" ( (~["\""]) | ( "<%" ( ~[">"] )+ "%>" ) )* "\"" ) > 
// Allow wrongly quoted attributes like VALUE=text".
| <#WRONGLY_QUOTED_STRING:
  ( (~[">", "\"", "'", " ", "\t", "\n", "\r"]) | ( "<%" ( ~[">"] )+ "%>" ) )+ ( ["\"", "'"] )? >
// CF strings escape quotes with two quotes.
| <#QUOTED_CFSTRING:
      ( "\"" ( (~["\""])+ | "\"" "\"" (~["\""])* )* "\"" )
    | ( "'"  ( (~["'"] )+ | "'"  "'"  (~["'"] )* )* "'"  )
  >
| <#NEWLINE:    ( /*"\r\n" |*/ "\r" | "\n" ) >
| <#WHITESPACE: ( " " | "\t" ) >
| <UNICODE_BOM: ("\uFEFF"|"\uFFFE") >
}

<DEFAULT> TOKEN :
{
  <CR_NEWLINE:     (<NEWLINE>)+ >
| <TAG_START:        "<"       > : StartTag
| <ENDTAG_START:     "</"      > : StartTag
| <CFCOMMENT_START:  "<!---"   > : InCfComment
| <COMMENT_START:    "<!--"    > : InComment
| <DECL_START:       "<!"      > : InDeclaration
| <PI_START:         "<?"      > : InProcessingInstruction
| <XSP_START:        "<%"      > : InXSPTag
//| <ENTITY_START:   "&"       > : InEntity
| <PCDATA:         (~["<", "\r", "\n", ">"])+ | ( (~["<", ">", "\r", "\n"])* "<https:" (~[">", "<"])* ">" (~["<", ">", "\r", "\n"])* )>
| <TAG_CLOSE:        ">"       >
}

<StartTag> TOKEN :
{
  <SCRIPT: "SCRIPT"> : InTag
| <STYLE:  "STYLE">  : InTag
| <JAVA:   "JAVA">   : InTag                      // JHTML <JAVA> tag
| <CFQUERY:  "CFQUERY">  : InCFTag                // <CFQUERY>SQL</>
| <CFSCRIPT: "CFSCRIPT"> : InCFTag                // <CFSCRIPT>cfscript</>
| <CF_EXPRESSIONTAG_NAME:
  // These tags take CF expressions instead of att=val pairs.
  // If you change these, also change ExtractionRules.isCFExpressionTag().
  ( "CFIF" | "CFELSEIF" | "CFSET" ) >  : InCFExpression
| <CF_TAG_NAME: "CF" <IDENTIFIER>   >  : InCFTag
| <TAG_NAME:         <IDENTIFIER>   >  : InTag
| <LST_ERROR: ~[]>   : DEFAULT
}

<AttrVal> SPECIAL_TOKEN :
{
  < (<WHITESPACE>|<NEWLINE>)+ >
}

<AttrVal> TOKEN :
{
  <ATTR_VALUE:
    ( 
    <QUOTED_STRING>
    | /* Allow wrongly quoted attributes like VALUE=text" */
      <WRONGLY_QUOTED_STRING> 
    )
  > /*: InTag*/
| <LAV_ERROR: ~[]>
}

<InTag, InCFTag, InCFExpression> SPECIAL_TOKEN :
{
  < (<WHITESPACE>|<NEWLINE>)+ >
}

<InTag> TOKEN :
{
  <TAG_START_EMBEDDED: "<" >            : StartEmbeddedTag
| <ENDTAG_START_EMBEDDED: "</" >        : StartEmbeddedTag
| <XSP_START_EMBEDDED: "<%" >           : StartEmbeddedTag
| <TAG_END: ">" >                       : DEFAULT
| <TAG_END_XSP: "%>" >                  : InTag
| <TAG_END_CLOSE: "/" (<WHITESPACE>|<NEWLINE>)* ">" > : DEFAULT
}

<InCFExpression, InCFTag> TOKEN :
{
  <CF_TAG_END: ">" >                 /* : DEFAULT */
| <CF_TAG_END_CLOSE: "/" (<WHITESPACE>|<NEWLINE>)* ">" > /* : DEFAULT */
}

<InTag, InCFTag> TOKEN :
{
  <ATTR_NAME: <IDENTIFIER> >
| <EQUAL: "=" >                         : AttrVal
| <LIT_ERROR: ~[]>
}

// Embedded tags can only be <CF> tags or <%.%> XSP sections.
// The XSP token should not match the empty string (endless loops).
<StartEmbeddedTag> TOKEN :
{
  <CF_EXPRESSIONTAG_NAME1:
  ( "CFIF" | "CFELSEIF" | "CFSET" )  >  : InCFExpression
| <CF_TAG_NAME1: "CF" <IDENTIFIER>   >  : InCFTag
| <XSP: (~["%"])+                    >
}

<InCFExpression> TOKEN :
{
  <CF_IDENTIFIER: <IDENTIFIER> >        : InCFExpression
| <CF_STRING    : <QUOTED_CFSTRING> >   : InCFExpression
| <CF_NUMBER    : (<NUM_CHAR>)+ >       : InCFExpression
| <CF_LPAREN    : "(" >                 : InCFExpression
| <CF_RPAREN    : ")" >                 : InCFExpression
| <CF_EQUAL     : "=" >                 : InCFExpression
| <CF_OPER : ["+","-","/","\\","*","^",",","#","&","[","]"] > : InCFExpression
| <CF_LIT_ERROR: ~[]>
}

<InScript> TOKEN :
{
  <SCRIPT_END: "</SCRIPT>" > : DEFAULT
| <SCRIPT_TEXT:  ~[] >
}

<InJava> TOKEN :
{
  <JAVA_END: "</JAVA>" > : DEFAULT
| <JAVA_TEXT:  ~[] >
}

<InCFScript> TOKEN :
{
  <CFSCRIPT_END:   "</CFSCRIPT>" > : DEFAULT
| <CFSCRIPT_TEXT:  ~[] >
}

<InCFQuery> TOKEN :
{
  <CFQUERY_END:   "</CFQUERY>" > : DEFAULT
| <CFQUERY_TEXT:  ~[] >
}

<InStyle> TOKEN :
{
  <STYLE_END:   "</STYLE>" > : DEFAULT
| <STYLE_TEXT:  ~[] >
}

<InStyle> MORE:
{
  < "<!--" > : InCommentedStyle
}

<InCommentedStyle> MORE:
{
  < ~[] >
}

<InCommentedStyle> TOKEN:
{
  < COMMENTED_STYLE_TEXT: "-->" > : InStyle
}

<InComment, InCfComment> SPECIAL_TOKEN :
{
  < (<NEWLINE>)+ >
}

<InComment> TOKEN :
{
  < COMMENT_END:  "-->" > : DEFAULT
| < DASH:         "-" >
| < COMMENT_WORD: (~["-"])+ >
}

<InCfComment> TOKEN :
{
  < CFCOMMENT_START1: "<!---" > : InCfComment
| < LESSTHAN:         "<" >
| < CFCOMMENT_END:    "--->" > /* Need to set lexical state manually. */
| < DASH1:            "-" >
| < COMMENT_WORD1:    (~["-", "<"])+ >
}

<InDeclaration> TOKEN :
{
  <DECL_ANY: (~[">"])+ >
| <DECL_END: ">" > : DEFAULT
}

<InProcessingInstruction> TOKEN :
{
  <PI_ANY: (~[">"])+ >
| <PI_END: ">" > : DEFAULT
}

<InXSPTag> TOKEN :
{
  <XSP_END: "%>" > : DEFAULT
| <XSP_TEXT: ~[] >
}

/*
<InEntity> TOKEN :
{
  <ENTITY_END:   ";" > : DEFAULT
| <NUMERIC_ENTITY:  ("#" ("x")? (["0"-"9", "a"-"f"])+) >
| <CHAR_ENTITY:  (<ALPHA_CHAR>)+ >
}
*/

/* START OF GRAMMAR */

void parse() :
{
    Token text;
    Tag t1;
    Script t2;
    Style t3;
    Java t4;
    CFTag t5;
    CFScript t6;
    CFQuery t7;
    Xsp t8;
    EndTag t9;
}
{
    { m_handler.handleStart(); }

    (<UNICODE_BOM>)?

    ( LOOKAHEAD(2) t1=parseStartTag()   { m_handler.handleStartTag(t1); }
    | LOOKAHEAD(2) t2=parseScript()     { m_handler.handleScript(t2); }
    | LOOKAHEAD(2) t3=parseStyle()      { m_handler.handleStyle(t3); }
    | LOOKAHEAD(2) t4=parseJava()       { m_handler.handleJava(t4); }
    | LOOKAHEAD(2) t5=parseCFStartTag() { m_handler.handleCFStartTag(t5); }
    | LOOKAHEAD(2) t6=parseCFScript()   { m_handler.handleCFScript(t6); }
    | LOOKAHEAD(2) t7=parseCFQuery()    { m_handler.handleCFQuery(t7); }
    | LOOKAHEAD(2) t8=parseXSP()        { m_handler.handleXsp(t8); }
    | t9=parseEndTag() {
                          if (t9.tag.toUpperCase().startsWith("CF"))
                          {
                            m_handler.handleCFEndTag(t9);
                          }
                          else
                          {
                            m_handler.handleEndTag(t9);
                          }
                       }
    | parseCfComment()
    | parseComment()
    | parseDeclaration()
    | parseProcessingInstruction()
    /*
    | LOOKAHEAD(2) <TAG_START> text=<LST_ERROR>
            {
            m_handler.handleText(new Text("<" + text.image));
            }
    */
    | parseText()
    | parseNewline()
	| parseSpecialChar()
    )*

    <EOF>

    { m_handler.handleFinish(); }
}

void parseComment() :
{
    Token t;
    StringBuffer s = new StringBuffer ("");
}
{
    <COMMENT_START>
    (
      t=<DASH>
            {
            s.append(t.image);
            }
    |
      t=<COMMENT_WORD>
            {
            s.append(t.image);
            }
    )*
    <COMMENT_END>
            {
            m_handler.handleComment(new Comment(s.toString()));
            }
}

void parseCfComment() :
{
    Token t;
    StringBuffer s;
}
{
    t=<CFCOMMENT_START>
    s=parseCfCommentRecursive(0)
        {
            m_handler.handleCfComment(new CfComment(s.toString()));
        }
}

/** Returns only the comment content without start and end tags. */
StringBuffer parseCfCommentRecursive(int level) :
{
    Token t;
    StringBuffer s = new StringBuffer();
    StringBuffer c;
}
{
    (
      <CFCOMMENT_START1>
      c=parseCfCommentRecursive(level + 1)
            {
            s.append("<!---");
            s.append(c);
            s.append("---!>");
            }
    |
      t=<LESSTHAN>
            {
            s.append(t.image);
            }
    |
      t=<DASH1>
            {
            s.append(t.image);
            }
    |
      t=<COMMENT_WORD1>
            {
            s.append(t.image);
            }
    )*
    <CFCOMMENT_END>
            {
            if (level == 0)
            {
                token_source.SwitchTo(DEFAULT);
            }
            return s;
            }
}

void parseProcessingInstruction() :
{
    Token t;
}
{
    <PI_START> t=<PI_ANY> <PI_END>
         {
         m_handler.handlePI(new PI(t.image));
         }
}

void parseDeclaration() :
{
    Token t;
}
{
    <DECL_START> t=<DECL_ANY> <DECL_END>
         {
         m_handler.handleDeclaration(new Declaration(t.image));
         }
}

Attribute parseAttribute(int p_state) :
{
    Token t1, t2 = null;
}
{
    t1=<ATTR_NAME> ( <EQUAL> t2=<ATTR_VALUE> )?
        {
        token_source.SwitchTo(p_state);

        if (t2 == null)
        {
            return new Attribute(t1.image);
        }
        else
        {
            String temp = t2.image;
            char lastChar  = temp.charAt(temp.length() - 1);
            char firstChar = temp.charAt(0);
            if ((lastChar != firstChar) &&
                (lastChar == '"' || lastChar == '\''))
            {
                // Must be one of them sickly quoted attributes.
                // Repair att=val" to att="val"
                temp = lastChar + temp;
            }

            // Handle CDATA: should ignore \n and replace \r\t with space.
            // However, we remove \r\n and leave \t alone.
            int i_at;
            while ((i_at = temp.indexOf('\n')) >= 0)
            {
                temp = temp.substring(0, i_at) + temp.substring(i_at + 1);
            }
            while ((i_at = temp.indexOf('\r')) >= 0)
            {
            temp = temp.substring(0, i_at) + temp.substring(i_at + 1);
            }

            return new Attribute(t1.image, temp);
        }
        }
}

AttributeList parseAttributeList() :
{
    AttributeList alist = new AttributeList();
    Attribute a;
}
{
    (
      a=parseAttribute(token_source.curLexState)
            {
            alist.addAttribute(a);
            }
    )*
      {
          return alist;
      }
}

ExtendedAttributeList parseExtendedAttributeList() :
{
    ExtendedAttributeList elist = new ExtendedAttributeList();
    AttributeList alist;
    Attribute a;
    Token t, t1, tag;
    EndTag e;
}
{
    (
      t=<TAG_START_EMBEDDED>
            {
            // shouldn't have to call SwitchTo() but only this works.
            token_source.SwitchTo(StartEmbeddedTag);
            }
      (
          tag=<CF_EXPRESSIONTAG_NAME1> alist=parseCFExpression()
      |
          tag=<CF_TAG_NAME1>           alist=parseAttributeList()
      )
      (
          t1=<CF_TAG_END>
              {
              elist.addSimpleTag(new CFTag(tag.image, alist, false,
                  getTokenText(t, t1)));
              token_source.SwitchTo(InTag);
              }
      |
          t1=<CF_TAG_END_CLOSE>
              {
              elist.addSimpleTag(new CFTag(tag.image, alist, true,
                  getTokenText(t, t1)));
              token_source.SwitchTo(InTag);
              }
      )
    |
      <XSP_START_EMBEDDED>
            {
            // shouldn't have to call SwitchTo() but only this works.
            token_source.SwitchTo(StartEmbeddedTag);
            }
      tag=<XSP>
            {
            // shouldn't have to call SwitchTo() but only this works.
            token_source.SwitchTo(InTag);
            }
      <TAG_END_XSP>
            {
            elist.addSimpleTag(new EmbeddedXspTag(tag.image,
                "<%" + tag.image + "%>"));
            }
    |
      <ENDTAG_START_EMBEDDED>
            {
            // shouldn't have to call SwitchTo() but only this works.
            token_source.SwitchTo(StartEmbeddedTag);
            }
      (
          tag=<CF_EXPRESSIONTAG_NAME1>
      |
          tag=<CF_TAG_NAME1>
      )
      <CF_TAG_END>
              {
              elist.addEndTag(new EndTag(tag.image));

              token_source.SwitchTo(InTag);
              }
    |
      a=parseAttribute(token_source.curLexState)
            {
            elist.addAttribute(a);
            }
    )*
      {
          return elist;
      }
}

Tag parseStartTag() :
{
    Token t=null, tag=null, t1;
    ExtendedAttributeList elist = null;
    Token firstToken = getToken(1);
    boolean b_closed = false;
}
{
    try
    {
        t=<TAG_START> tag=<TAG_NAME> elist=parseExtendedAttributeList()
        (
          t1=<TAG_END>
        | t1=<TAG_END_CLOSE>       { b_closed = true; }
        )
        {
        token_source.SwitchTo(DEFAULT);

        return new Tag(tag.image, elist, b_closed,
            getTokenText(t, t1), t.beginLine, t.beginColumn);
        }
    }
    catch (ParseException e)
    {
        token_source.SwitchTo(DEFAULT);

        if(!ignoreInvalidHtmlTags)
        {
            String s = getTokenText(firstToken, getNextToken());
            throw new ParseException("Error parsing STARTTAG:\n" +
                e.getMessage() + "\nin input: " + s);
        }

        Token t2 = getNextToken();
        while (t2.kind != TAG_CLOSE && !">".equalsIgnoreCase(t2.image))
        {
            t2 = getNextToken();
            
            if (t2.kind == EOF)
            {
                String s = getTokenText(firstToken, t2);
                {if (true) throw new ParseException("Error parsing STARTTAG:\n"
                        + e.getMessage() + "\nin input: " + s);}
            }
        }
        
        String content = getTokenText(t, t2);
        b_closed = content.endsWith("/>");
        Tag returnTag = new Tag(tag.image, elist, b_closed,
                content, t.beginLine, t.beginColumn);
        returnTag.setIgnore(true);
        
        return returnTag;
    }
}

Attribute parseCFToken() :
{
    Token t = null;
    StringBuffer sb = new StringBuffer();
}
{
    (
        t=<CF_IDENTIFIER>
      | t=<CF_STRING>
      | t=<CF_NUMBER>
      | t=<CF_LPAREN>
      | t=<CF_RPAREN>
      | t=<CF_OPER>
      | t=<CF_EQUAL>
    )
    {
        if (t.specialToken != null)
        {
            Token tt = t.specialToken;
            while (tt.specialToken != null)
            {
                tt = tt.specialToken;
            }

            for (; tt != null; tt = tt.next)
            {
                sb.append(tt.image);
            }
        }

        sb.append(t.image);

        return new Attribute(sb.toString());
    }
}

// For expression-taking tags (CFSET, CFIF, CFELSEIF) we read tokens
// until we hit the closing > tag delimiter.  The tokens are collected
// as Attribute.name fields in an AttributeList which is then
// converted to a single string in a new Attribute.name field.  That
// way it can be submitted to a CFScript parser later.
//
// Note: whitespace in expressions is lost.
AttributeList parseCFExpression() :
{
    AttributeList alist = new AttributeList();
    Attribute a;
}
{
    (
      a=parseCFToken()
            {
            alist.addAttribute(a);
            }
    )*
      {
          // Restore the original string and put it into a single attr
          StringBuffer buf = new StringBuffer();

          for (int i = 0; i < alist.size(); ++i)
          {
              a = (Attribute)alist.get(i);
              buf.append(a.name);
          }

          alist.clear();
          alist.addAttribute(new Attribute(buf.toString()));

          return alist;
      }
}

CFTag parseCFStartTag() :
{
    Token t, t1, tag;
    AttributeList alist;
    Token firstToken = getToken(1);
    boolean b_closed = false;
}
{
    try
    {
        t=<TAG_START>
        (
            tag=<CF_EXPRESSIONTAG_NAME> alist=parseCFExpression()
        |
            tag=<CF_TAG_NAME> alist=parseAttributeList()
        )
        (
          t1=<CF_TAG_END>
                {
                token_source.SwitchTo(DEFAULT);
                }
        | t1=<CF_TAG_END_CLOSE>
                {
                token_source.SwitchTo(DEFAULT);
                b_closed = true;
                }
        | t1=<TAG_END>
                {
                token_source.SwitchTo(DEFAULT);
                }
        | t1=<TAG_END_CLOSE>
                {
                token_source.SwitchTo(DEFAULT);
                }
        )
        {
        return new CFTag(tag.image, alist, b_closed,
            getTokenText(t, t1));
        }
    }
    catch (ParseException e)
    {
        token_source.SwitchTo(DEFAULT);
        String s = getTokenText(firstToken, getNextToken());
        throw new ParseException("Error parsing CFSTARTTAG:\n" +
            e.getMessage() + "\nin input: " + s);
    }
}

CFScript parseCFScript() :
{
    Token t, t1, t2, t3, tag;
    StringBuffer s = new StringBuffer();
    AttributeList alist;
}
{
    t=<TAG_START> tag=<CFSCRIPT> alist=parseAttributeList()
    (
      t1=<CF_TAG_END_CLOSE>
            {
            token_source.SwitchTo(DEFAULT);

            return new CFScript(tag.image, alist, true,
                getTokenText(t, t1), "", t.beginLine, t.beginColumn);
            }
    | t1=<CF_TAG_END>
            {
            token_source.SwitchTo(InCFScript);
            }
      (
        t2=<CFSCRIPT_TEXT>   { s.append(t2.image); }
      )*
      t3=<CFSCRIPT_END>
            {
            String text = s.toString();

            return new CFScript(tag.image, alist, false,
                getTokenText(t, t3), text, t.beginLine, t.beginColumn);
            }
    )
}

CFQuery parseCFQuery() :
{
    Token t, t1, t2, t3, tag;
    StringBuffer s = new StringBuffer();
    AttributeList alist;
}
{
    t=<TAG_START> tag=<CFQUERY> alist=parseAttributeList()
    (
      t1=<CF_TAG_END_CLOSE>
            {
            token_source.SwitchTo(DEFAULT);

            return new CFQuery(tag.image, alist, true,
                getTokenText(t, t1), "", t.beginLine, t.beginColumn);
            }
    | t1=<CF_TAG_END>
            {
            token_source.SwitchTo(InCFQuery);
            }
      (
        t2=<CFQUERY_TEXT>    { s.append(t2.image); }
      )*
      t3=<CFQUERY_END>
            {
            String text = s.toString();

            return new CFQuery(tag.image, alist, false,
                getTokenText(t, t3), text, t.beginLine, t.beginColumn);
            }
     )
}

Xsp parseXSP() :
{
    Token t, t1, t2;
    StringBuffer s = new StringBuffer();
}
{
    t=<XSP_START>
    (
      t1=<XSP_TEXT> { s.append(t1.image); }
    )*
    t2=<XSP_END>
            {
            String text = s.toString();

            return new Xsp(text, t.beginLine, t.beginColumn);
            }
}

Script parseScript() :
{
    Token t, t1, t2, t3, tag;
    StringBuffer b = new StringBuffer();
    ExtendedAttributeList elist;
}
{
    t=<TAG_START> tag=<SCRIPT> elist=parseExtendedAttributeList()
    (
      t1=<TAG_END_CLOSE>
            {
            return new Script(tag.image, elist, true,
                getTokenText(t, t1), "", t.beginLine, t.beginColumn);
            }
    | t1=<TAG_END>          { token_source.SwitchTo(InScript); }
      (
        t2=<SCRIPT_TEXT>    { b.append(t2.image); }
      )*
      t3=<SCRIPT_END>
            {
            String text = b.toString();

            return new Script(tag.image, elist, false,
                getTokenText(t, t3), text, t.beginLine, t.beginColumn);
            }
    )
}

Java parseJava() :
{
    Token t, t1, t2, t3, tag;
    StringBuffer b = new StringBuffer();
    ExtendedAttributeList elist;
}
{
    t=<TAG_START> tag=<JAVA> elist=parseExtendedAttributeList()
    (
      t1=<TAG_END_CLOSE>
            {
            return new Java(tag.image, elist, true,
                getTokenText(t, t1), "", t.beginLine, t.beginColumn);
            }
    | t1=<TAG_END>          { token_source.SwitchTo(InJava); }
      (
        t2=<JAVA_TEXT>      { b.append(t2.image); }
      )*
      t3=<JAVA_END>
            {
            String text = b.toString();

            return new Java(tag.image, elist, false,
                getTokenText(t, t3), text, t.beginLine, t.beginColumn);
            }
    )
}

Style parseStyle() :
{
    Token t, t1, t2, t3, tag;
    StringBuffer b = new StringBuffer();
    ExtendedAttributeList elist;
}
{
    t=<TAG_START> tag=<STYLE> elist=parseExtendedAttributeList()
    (
      t1=<TAG_END_CLOSE>
            {
            return new Style(tag.image, elist, true,
                getTokenText(t, t1), "", t.beginLine, t.beginColumn);
            }
    | t1=<TAG_END>                { token_source.SwitchTo(InStyle); }
      (
        t2=<STYLE_TEXT>           { b.append(t2.image); }
      |
        t2=<COMMENTED_STYLE_TEXT> { b.append(t2.image); }
      )*
      t3=<STYLE_END>
            {
            String text = b.toString();

            return new Style(tag.image, elist, false,
                getTokenText(t, t3), text, t.beginLine, t.beginColumn);
            }
    )
}

EndTag parseEndTag() :
{
    Token t;
}
{
    <ENDTAG_START>
    (
      t=<TAG_NAME> <TAG_END>
    |
      t=<CF_TAG_NAME> <CF_TAG_END>
            {
            token_source.SwitchTo(DEFAULT);
            }
    |
      t=<CF_EXPRESSIONTAG_NAME> <CF_TAG_END>
            {
            token_source.SwitchTo(DEFAULT);
            }
    )
        {
            return new EndTag(t.image);
        }
}

void parseText() :
{
    Token t;
}
{
    t=<PCDATA>
        {
        m_handler.handleText(new Text(t.image));
        }
}

void parseSpecialChar() :
{
    Token t;
}
{
    t=<TAG_CLOSE>
        {
        m_handler.handleSpecialChar(new Text(t.image));
        }
}

void parseNewline() :
{
    Token t;
}
{
    t=<CR_NEWLINE>
        {
        m_handler.handleNewline(new Newline(t.image));
        }
}

/*
void parseEntity() : { }
{
<ENTITY_START> (<NUMERIC_ENTITY> | <CHAR_ENTITY>) <ENTITY_END>

{entity();}
}
*/